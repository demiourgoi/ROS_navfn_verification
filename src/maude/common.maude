fmod SIGN is
 pr FLOAT .

 var V : Float .

 op sign : Float -> Float .
 eq sign(0.0) = 0.0 .
 ceq sign(V) = - 1.0
  if V < 0.0 .
 ceq sign(V) = 1.0
  if V > 0.0 .
endfm

fmod COSTMAP is
 pr CONVERSION .
 pr INT .

 vars X Y NC NR N N' : Int .
 var  F FX FY FNC : Float .
 vars NL NL' : IntList .
 var  CM : CostMap .
 vars S S' : String .
 var  NT : Nat .

 sort IntList CostMap .
 subsort Int < IntList .

 op mtIL : -> IntList [ctor] .
 op _,_ : IntList IntList -> IntList [ctor assoc id: mtIL] .
 op {_} : IntList -> CostMap [ctor] .

 op |_| : IntList -> Nat .
 eq | N | = 1 .
 eq | N, NL | = s(| NL |) .

 ***        CostMap   X     Y   NCOLS
 op open? : CostMap Float Float  Float  -> Bool [memo] .
 ceq open?(CM, FX, FY, FNC) = open?(CM, X, Y, NC)
  if X := float2nat(FX) /\
     Y := float2nat(FY) /\
     NC := float2nat(FNC) .

 op open? : CostMap Nat Nat Nat  -> Bool .
 eq open?(CM, X, Y, NC) = check(get(CM, X, Y, NC)) .

 *** Uses faster Python version if available
 *** op get : CostMap Nat Nat Nat -> Float .
 op get : CostMap Nat Nat Nat -> Float [special (id-hook SpecialHubSymbol)] .
 ceq get(CM, X, Y, NC) = float(N')
  if N := (NC * X) + Y /\
     N' := skipN&Get(CM, N) .

 op skipN&Get : CostMap Nat -> Int .
 eq skipN&Get({N, NL'}, 0) = N .
 eq skipN&Get({N, NL'}, s(NT)) = skipN&Get({NL'}, NT) .
 eq skipN&Get(CM, NT) = - 1 [owise] .

 op check : Float -> Bool .
 eq check(F) = F < 254.0 .

 ops movCost diagMovCost cellCost obstacleCost mapCost costNeutral stepSize : -> Float .
 eq movCost = 100.0 .
 eq diagMovCost = 140.0 .
 eq cellCost = 50.0 .
 eq obstacleCost = 254.0 .
 eq mapCost = 50.0 .
 eq costNeutral = 50.0 .
 eq stepSize = 0.5 .

 op float2nat : Float -> Nat [memo] .
 eq float2nat(F) = rat(floor(F)) .
endfm

fmod POSE is
 pr EXT-BOOL .
 pr COSTMAP .
 pr SIGN .
 pr INT .

 vars N NR NC X Y Z X' Y' Z' : Nat .
 vars Q Q' : Quaternion .
 vars H V FX FY DX DY : Float .
 var  CM : CostMap .
 var  NOW : Pose .

 sorts BasicPose Pose Quaternion Point .

 *** Simplified representation for angles
 subsort Int < Quaternion .

 *** Simplified pose for 2D
 subsort BasicPose < Pose .

 op getCost : Int ~> Float [memo] .
 eq getCost(N) = if (N rem 90) == 0
                 then movCost
                 else diagMovCost
                 fi .

 op {_,_,_} : Nat Nat Nat -> Point [ctor] .
 op {_,_,_} : Float Float Float -> Point [ctor] .
 op {_,_,_,_} : Float Float Float Float -> Quaternion [ctor] .
 op __ : Point Quaternion -> Pose [ctor] .

 op {_,_} : Nat Nat -> BasicPose [ctor] .
 op {_,_} : Float Float -> BasicPose [ctor] .

 op noPose : -> Pose [ctor] .

 op h : Pose Pose -> Float .
 ceq h({X, Y, Z} Q, {X', Y', Z'} Q') = sqrt((H ^ 2.0) + (V ^ 2.0)) * movCost
  if H := float(sd(X, X')) /\
     V := float(sd(Y, Y')) .
 ceq h({X, Y}, {X', Y'}) = sqrt((H ^ 2.0) + (V ^ 2.0)) * movCost
  if H := float(sd(X, X')) /\
     V := float(sd(Y, Y')) .

 op closest : BasicPose Float Float ~> BasicPose .
 eq closest({FX, FY}, DX, DY) = closest({FX + sign(DX) * closest(abs(DX)), FY + sign(DY) * closest(abs(DY))}) .

 op closest : BasicPose ~> BasicPose .
 eq closest({FX, FY}) = {float2nat(FX), float2nat(FY)} .

 op closest : Float -> Float .
 eq closest(FX) = if _-_(ceiling(FX), FX) <= _-_(FX, floor(FX))
                  then ceiling(FX)
                  else floor(FX)
                  fi .
endfm

view Pose from TRIV to POSE is
 sort Elt to Pose .
endv

fmod POTENTIAL is
 pr POSE .
 pr INT .

 ops infinite minusInfinite : -> Float .
 eq infinite = 1e10 .
 eq minusInfinite = - 1e10 .

 sort Row Potential .
 subsort Float < Row < Potential .

 op noNum : -> Row [ctor] .
 op _._ : Row Row -> Row [ctor assoc id: noNum] .

 op noRow : -> Potential [ctor] .
 op __ : Potential Potential -> Potential [ctor assoc id: noRow] .

 vars N N' N1 N2 NR NC X Y : Nat .
 vars I I' F F' : Float .
 vars P P' : Potential .
 vars R R' : Row .
 var  NOW : Pose .

 op _@[_,_] : Potential Nat Nat ~> Float .
 eq (R P) @ [0, N] = R [N] .
 eq (R P) @ [s(N), N'] = P @ [N, N'] .

 op _[_] : Row Nat ~> Float .
 eq (I . R) [0] = I .
 eq (I . R) [s(N)] = R [N] .

 op _[_;_\\_] : Potential Nat Nat Float ~> Potential .
 eq (R P) [0 ; N \\ I] = (R [N \\ I]) P .
 eq (R P) [s(N) ; N' \\ I] = R (P [N ; N' \\ I]) .

 op _[_\\_] : Row Nat Float ~> Row .
 eq (I . R) [0 \\ I'] = I' . R .
 eq (I . R) [s(N) \\ I'] = I . (R [N \\ I']) .

 ***          Rows Cols  Init Init
 op initialP* : Nat  Nat   Nat Nat  -> Potential .
 ceq initialP*(N, N', N1, N2) = P [N1 ; N2 \\ 0.0]
  if P := initialP(N, N') .

 op initialP : Nat Nat -> Potential .
 eq initialP(0, N') = noRow .
 eq initialP(s(N), N') = initialRow(N') initialP(N, N') .

 op initialRow : Nat -> Row [memo] .
 eq initialRow(0) = noNum .
 eq initialRow(s(N)) = infinite . initialRow(N) .

 *** It returns true if all exist and are open
 op allNeighborsOpen : Potential Pose Nat Nat -> Bool .
 eq allNeighborsOpen(P, NOW, NR, NC) = $allNeighborsOpen(P, NOW, NR, NC, 0) .

 op $allNeighborsOpen : Potential Pose Nat Nat Nat -> Bool .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 0) =
               if (s(Y) < NC)
               then (P @[X, s(Y)] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 45) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 45) =
               if (X > 0) and (s(Y) < NC)
               then (P @[sd(X, 1), s(Y)] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 90) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 90) =
               if X > 0
               then (P @[sd(X, 1), Y] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 135) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 135) =
               if X > 0 and Y > 0
               then (P @[sd(X, 1), sd(Y, 1)] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 180) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 180) =
               if Y > 0
               then (P @[X,  sd(Y, 1)] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 225) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 225) =
               if (s(X) < NR) and (Y > 0)
               then (P @[s(X),  sd(Y, 1)] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 270) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 270) =
               if (s(X) < NR)
               then (P @[s(X),  Y] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 315) .
 eq $allNeighborsOpen(P, {X, Y}, NR, NC, 315) =
               if (s(X) < NR) and (s(Y) < NC)
               then (P @[s(X), s(Y)] < infinite)
               else false
               fi and-then
               $allNeighborsOpen(P, {X, Y}, NR, NC, 360) .
 eq $allNeighborsOpen(P, NOW, NR, NC, N) = true [owise] .
endfm

fmod PATH is
 inc LIST{Pose} * (sort List{Pose} to Path,
                   sort NeList{Pose} to NePath,
                   op nil to noPath) .
endfm

fmod POSE-SET is
 pr SET{Pose} * (sort Set{Pose} to PoseSet) .

 eq (P:Pose, P:Pose) = P:Pose .

 vars X Y N N' : Nat .
 var  PS : PoseSet .

 op createComplete : Nat Nat -> PoseSet .
 eq createComplete(0, N) = empty .
 eq createComplete(s(N), N') = createComplete*(N, N'),
                               createComplete(N, N') .

 op createComplete* : Nat Nat -> PoseSet .
 eq createComplete*(N, 0) = empty .
 eq createComplete*(N, s(N')) = {N, N'}, createComplete*(N, N') .

 op _\[_,_] : PoseSet Nat Nat -> PoseSet .
 eq ({X, Y}, PS)\[X, Y] = PS .
 eq PS \[X, Y] = PS [owise] .
endfm

fmod GRADIENT is
 pr FLOAT .
 pr NAT .

 sort GCell GRow Gradient .
 subsort GCell < GRow < Gradient .

 op <_,_> : Float Float -> GCell [ctor] .

 op mtGR : -> GRow [ctor] .
 op __ : GRow GRow -> GRow [ctor assoc id: mtGR] .

 op mtGradient : -> Gradient [ctor] .
 op _,_ : Gradient Gradient -> Gradient [ctor assoc id: mtGradient] .

 vars X Y N N' : Nat .
 vars GC GC' : GCell .
 var  G : Gradient .
 var  GR : GRow .
 var  F : Float .

 op _[[_,_]] : Gradient Nat Nat -> GCell .
 eq (GR, G) [[0, Y]] = GR [Y] .
 eq (GR, G) [[s(X), Y]] = G [[X, Y]] .
 eq G [[X,Y]] = < 0.0, 0.0 > [owise] .

 op _[_] : GRow Nat -> GCell .
 eq (GC GR) [0] = GC .
 eq (GC GR) [s(Y)] = GR [Y] .
 eq GR [Y] = < 0.0, 0.0 > [owise] .

 op initialGradient : Nat Nat -> Gradient .
 eq initialGradient(0, N') = mtGradient .
 eq initialGradient(s(N), N') = initialGRow(N'), initialGradient(N, N') .

 op initialGRow : Nat -> GRow [memo] .
 eq initialGRow(0) = mtGR .
 eq initialGRow(s(N)) = < 0.0, 0.0 > initialGRow(N) .

 op _|_,_|->_ : Gradient Nat Nat GCell -> Gradient .
 eq (GR, G) | 0, Y |-> GC = (GR [Y] -> GC), G .
 eq (GR, G) | s(X), Y |-> GC = GR, (G | X, Y |-> GC) .
 eq G | X,Y |-> GC = G [owise] .

 op _[_]->_ : GRow Nat GCell -> GRow .
 eq (GC GR) [0] -> GC' = GC' GR .
 eq (GC GR) [s(Y)] -> GC' = GC (GR [Y] -> GC') .
 eq GR [Y] -> GC' = GR [owise] .
endfm

fmod BASIC-TRAVERSE is
 pr POTENTIAL .
 pr LIST{Nat} .
 pr POSE-SET .
 pr PATH .

 vars H V V' T MINV D DX DY SS FX FY FX' FY' FX'' FY'' : Float .
 vars X Y Z X' Y' Z' XI YI XC YC XN YN NR NC I NEW N : Nat .
 vars INIT GOAL ACCP NOW : Pose .
 vars Q Q' : Quaternion .
 vars P P' : Potential .
 var  LN : List{Nat} .
 var  CM : CostMap .
 var  PT : Path .

 *** Basic computation without gradient
 op computePath : Pose Pose Potential Nat Nat -> Path .
 ceq computePath({X, Y, Z} Q, GOAL, P, NR, NC) = noPath
  if P @ [X, Y] == infinite .
 eq computePath({X, Y, Z} Q, GOAL, P, NR, NC) = $computePath({X, Y}, GOAL, P, NR, NC, noPath) [owise] .

 op $computePath : Pose Pose Potential Nat Nat Path -> Path .
 eq $computePath({X, Y}, {X, Y, Z} Q, P, NR, NC, PT) = PT ({X, Y, Z} Q) .
 ceq $computePath({X, Y}, GOAL, P, NR, NC, PT) = $computePath(ACCP, GOAL, P, NR, NC, PT ({X, Y, 0} 0))
  if ACCP := getMin({X, Y}, P, NR, NC) [owise] .

 op getMin : Pose Potential Nat List{Nat} -> Pose .
 eq getMin(INIT, P, NR, NC) = getMin(INIT, P, NR, NC, noPose, infinite, 135 180 225 90 270 45 0 315) .

 op getMin : Pose Potential Nat Nat Pose Float List{Nat} -> Pose .
 ceq getMin({X, Y}, P, NR, NC, ACCP, MINV, N LN) = if V < MINV
                                                   then getMin({X, Y}, P, NR, NC, {X', Y'}, V, LN)
                                                   else getMin({X, Y}, P, NR, NC, ACCP, MINV, LN)
                                                   fi
  if < X', Y' > := getMove({X, Y}, N, NR, NC) /\
     V := (P @ [X', Y']) .
 ceq getMin({X, Y}, P, NR, NC, ACCP, MINV, N LN) = getMin({X, Y}, P, NR, NC, ACCP, MINV, LN)
  if not (getMove({X, Y}, N, NR, NC) :: Pair) .
 eq getMin(INIT, P, NR, NC, ACCP, MINV, nil) = ACCP .

 sort Pair .
 op <_,_> : Nat Nat -> Pair [ctor] .
 op getMove : Pose Nat Nat Nat ~> Pair .
 ceq getMove({X, Y}, 0, NR, NC)   = <  X, s(Y) >
  if s(Y) < NC .
 ceq getMove({X, Y}, 45, NR, NC)  = <  sd(X, 1), s(Y) >
  if X > 0 /\
     s(Y) < NC .
 ceq getMove({X, Y}, 90, NR, NC)  = <  sd(X, 1), Y >
  if X > 0 .
 ceq getMove({X, Y}, 135, NR, NC) = < sd(X, 1), sd(Y, 1) >
  if X > 0 /\
     Y > 0 .
 ceq getMove({X, Y}, 180, NR, NC) = < X,  sd(Y, 1) >
  if Y > 0 .
 ceq getMove({X, Y}, 225, NR, NC) = < s(X),  sd(Y, 1) >
  if s(X) < NR /\
     Y > 0 .
 ceq getMove({X, Y}, 270, NR, NC) = <  s(X),  Y >
  if s(X) < NR .
 ceq getMove({X, Y}, 315, NR, NC) = <  s(X),  s(Y) >
  if s(X) < NR /\
     s(Y) < NC .
endfm

fmod GRADIENT-TRAVERSAL is
 pr BASIC-TRAVERSE .
 pr POTENTIAL .
 pr GRADIENT .
 pr POSE-SET .
 pr PATH .
 pr SIGN .

 vars V T D DX DY DX' DY' DX'' DY'' DDX DDY SS FX FY FX' FY' FX'' FY'' NORM EX SX SEX EY SY SEY CX CY XX YY DXAUX1 DXAUX2 DYAUX1 DYAUX2 : Float .
 vars X Y Z X' Y' Z' XI YI XC YC XN YN NR NC I NEW N : Nat .
 vars G G' G1 G2 G3 : Gradient .
 vars INIT GOAL : Pose .
 vars CURR : PoseSet .
 var  P : Potential .
 var  CM : CostMap .
 var  PT : Path .

 ***                                INIT GOAL STEP SIZE
 op computePath : Potential Pose Pose   Float   Gradient Nat Nat Nat -> Path .
 eq computePath(P, {X, Y}, {X', Y'}, SS, G, NR, NC, N) = if P @ [X, Y] == infinite then noPath else
      computePath(P, {float(X'), float(Y')}, SS, G, NR, NC, N, {float(X), float(Y)}, 0.0, 0.0, noPath) fi .

 ***                                GOAL STEP SIZE                            DX    DY    ACC
 op computePath : Potential Pose   Float   Gradient Nat Nat Nat Pose Float Float Path -> Path .
 eq computePath(P, GOAL, SS, G, NR, NC, 0, CURR, DX, DY, PT) = noPath .
 ceq computePath(P, {FX, FY}, SS, G, NR, NC, s(N), {FX', FY'}, DX, DY, PT) = PT ({FX, FY, 0.0} 0)
  if {X, Y} := closest({FX', FY'}, DX, DY) /\
     (P @ [X, Y]) < costNeutral [print "Eq2 " FX' ", " FY'] .
 ceq computePath(P, GOAL, SS, G, NR, NC, s(N), {FX, FY}, DX, DY, PT) =
                               computePath(P, GOAL, SS, G, NR, NC, N, {float(X'), float(Y')}, 0.0, 0.0, PT ({FX', FY', 0.0} 0))
  if FX' := FX + DX /\
     FY' := FY + DY /\
     {X, Y} := closest({FX, FY}, DX, DY) /\
     (P @ [X, Y]) >= costNeutral /\
     not allNeighborsOpen(P, {float2nat(FX),float2nat(FY)}, NR, NC) /\
     {X', Y'} := getMin({X, Y}, P, NR, NC) [print "Eq3 " FX' ", " FY'] .
 ceq computePath(P, GOAL, SS, G, NR, NC, s(N), {FX, FY}, DX, DY, PT) = noPath
  if X' := float2nat(FX) /\
     Y' := float2nat(FY) /\
     {X, Y} := closest({FX, FY}, DX, DY) /\
     (P @ [X, Y]) >= costNeutral /\
     allNeighborsOpen(P, {X',Y'}, NR, NC) /\
     < CX, CY, G1 > := update&get(G, P, X', Y', NR, NC) /\
     < SX, SY, G2 > := update&get(G1, P, s(X'), Y', NR, NC) /\
     < SEX, SEY, G3 > := update&get(G2, P, s(X'), s(Y'), NR, NC) /\
     < EX, EY, G' > := update&get(G3, P, X', s(Y'), NR, NC) /\
     DDX := _-_(1.0, DX) /\
     DDY := _-_(1.0, DY) /\
     XX := (DDY * (DDX * CX) + (DX * EX)) + DY * ((DDX * SX) + (DX * SEX)) /\
     YY := (DDY * (DDX * CY) + (DX * EY)) + DY * ((DDX * SY) + (DX * SEY)) /\
     XX == 0.0 /\
     YY == 0.0 [print "Eq4 " FX ", " FY] .
 ceq computePath(P, GOAL, SS, G, NR, NC, s(N), {FX, FY}, DX, DY, PT) =
                               computePath(P, GOAL, SS, G', NR, NC, N, {FX'', FY''}, DX'', DY'', PT ({FX', FY', 0.0} 0))
  if FX' := FX + DX /\
     FY' := FY + DY /\
     X' := float2nat(FX) /\
     Y' := float2nat(FY) /\
     {X, Y} := closest({FX, FY}, DX, DY) /\
     (P @ [X, Y]) >= costNeutral /\
     allNeighborsOpen(P, {X',Y'}, NR, NC) /\
     < CX, CY, G1 > := update&get(G, P, X', Y', NR, NC) /\
     < SX, SY, G2 > := update&get(G1, P, s(X'), Y', NR, NC) /\
     < SEX, SEY, G3 > := update&get(G2, P, s(X'), s(Y'), NR, NC) /\
     < EX, EY, G' > := update&get(G3, P, X', s(Y'), NR, NC) /\
     DDX := _-_(1.0, DX) /\
     DDY := _-_(1.0, DY) /\
     XX := (DDY * (DDX * CX) + (DX * EX)) + DY * ((DDX * SX) + (DX * SEX)) /\
     YY := (DDY * (DDX * CY) + (DX * EY)) + DY * ((DDX * SY) + (DX * SEY)) /\
     ((XX =/= 0.0) or (YY =/= 0.0)) /\
     DX' := DX + XX * (SS / sqrt((XX * XX) + (YY * YY))) /\
     DY' := DY + YY * (SS / sqrt((XX * XX) + (YY * YY))) /\
     FX'' := if abs(DX') > 1.0
             then FX + sign(DX')
             else FX
             fi /\
     DX'' := if abs(DX') > 1.0
             then _-_(DX', sign(DX'))
             else DX'
             fi /\
     FY'' := if abs(DY') > 1.0
             then FY + sign(DY')
             else FY
             fi /\
     DY'' := if abs(DY') > 1.0
             then _-_(DY', sign(DY'))
             else DY'
             fi [print "Eq5 " FX' ", " FY' " con " FX ", " FY " y " DX ", " DY] .

 sort GradientUpdateRes .
 op <_,_,_> : Float Float Gradient -> GradientUpdateRes [ctor] .

 op update&get : Gradient Potential Nat Nat Nat Nat -> GradientUpdateRes .
 ceq update&get(G, P, X, Y, NC, NR) = < FX, FY, G >
  if < FX, FY > := (G [[X, Y]]) /\
     < FX, FY > =/= < 0.0, 0.0 > .
 ceq update&get(G, P, X, Y, NC, NR) = < 0.0, 0.0, G >
  if (G [[X, Y]]) == < 0.0, 0.0 > /\
     ((X == 0) or (s(X) == NR) or (Y == 0) or (s(Y) == NC)) .
 ceq update&get(G, P, X, Y, NC, NR) = < DX', DY', G' >
  if G [[X, Y]] == < 0.0, 0.0 > /\
     ((X =/= 0) and (s(X) =/= NR) and (Y =/= 0) and (s(Y) =/= NC)) /\
     (P @ [X, Y]) == infinite /\
     DX := if (P @ [sd(X, 1), Y]) < infinite
           then - obstacleCost
           else if (P @ [s(X), Y]) < infinite
                then obstacleCost
                else 0.0
                fi
           fi /\
     DY := if (P @ [X, sd(Y, 1)]) < infinite
           then - obstacleCost
           else if (P @ [X, s(Y)]) < infinite
                then obstacleCost
                else 0.0
                fi
           fi /\
     NORM := sqrt((DX * DX) + (DY * DY)) /\
     DX' := if NORM > 0.0 then DX / NORM else DX fi /\
     DY' := if NORM > 0.0 then DY / NORM else DY fi /\
     G' := (G | X, Y |-> < DX', DY' >) .
 ceq update&get(G, P, X, Y, NC, NR) = < DX', DY', G' >
  if G [[X, Y]] == < 0.0, 0.0 > /\
     ((X =/= 0) and (s(X) =/= NR) and (Y =/= 0) and (s(Y) =/= NC)) /\
     (P @ [X, Y]) < infinite /\
     DXAUX1 := if (P @ [s(X), Y]) < infinite
               then _-_((P @ [X, Y]), (P @ [s(X), Y]))
               else 0.0
               fi /\
     DXAUX2 := if (P @ [sd(X, 1), Y]) < infinite
               then _-_((P @ [sd(X, 1), Y]), (P @ [X, Y]))
               else 0.0
               fi /\
     DX := DXAUX1 + DXAUX2 /\
     DYAUX1 := if (P @ [X, sd(Y, 1)]) < infinite
               then _-_((P @ [X, sd(Y, 1)]), (P @ [X, Y]))
               else 0.0
               fi /\
     DYAUX2 := if (P @ [X, s(Y)]) < infinite
               then _-_((P @ [X, Y]), (P @ [X, s(Y)]))
               else 0.0
               fi /\
     DY := DYAUX1 + DYAUX2 /\
     NORM := sqrt((DX * DX) + (DY * DY)) /\
     DX' := if NORM > 0.0 then DX / NORM else DX fi /\
     DY' := if NORM > 0.0 then DY / NORM else DY fi /\
     G' := (G | X, Y |-> < DX', DY' >) .
endfm

eof

set print attribute on .

red
computePath({50,50,50,50,50,50,50,50,50},
            (1.2724633791999999e+2 . 8.5199999999999989e+1 . 5.0e+1)
            (1.0e+2 . 5.0e+1 . 0.0)
            (1.0e+10 . 8.5199999999999989e+1 . 5.0e+1),
            {1.0,2.0}, 5.0e-1,
            ((< 0.0,0.0 > < 0.0,0.0 > < 0.0,0.0 >),
             (< 0.0,0.0 > < 0.0,0.0 > < 0.0,0.0 >),
             (< 0.0,0.0 > < 0.0,0.0 > < 0.0,0.0 >)), 3, 3, 99, {1.0,1.0}, 0.0, 0.0,
            {0.0,0.0,0.0} 0) .


